# List recipes.
help:
    @just -l -u

qb := 'quickbench'
#qb := 'quickbench -N2 -n2 -p3'

#time := 'time'
time := 'gtime'

tackler := 'tackler --config tackler.toml --input.fs.ext journal'

beanbal := '"SELECT account, sum(position) ORDER BY account;"'

# Run the benchmarks with quickbench.
bench:
    {{ qb }} \
    'ledger  -f files1/100k.journal bal' \
    'hledger -f files1/100k.journal bal' \
    'bean-query files1/100k.beancount {{ beanbal }}' \
    '{{ tackler }} --input.fs.dir files1'

# Run the benchmarks with (g)time.
bench2:
    {{ time }} ledger  -f files1/100k.journal bal >/dev/null; echo
    {{ time }} hledger -f files1/100k.journal bal >/dev/null; echo
    {{ time }} bean-query files1/100k.beancount {{ beanbal }} >/dev/null; echo
    {{ time }} {{ tackler }} --input.fs.dir files1  >/dev/null; echo

# Save the outputs generated by the benchmark commands, for sanity checking.
bench-outputs:
    mkdir -p outputs
    ledger  -f files1/100k.journal bal    >outputs/balances-ledger.txt
    hledger -f files1/100k.journal bal    >outputs/balances-hledger.txt
    bean-query files1/100k.beancount {{ beanbal }} >outputs/balances-beancount.txt
    {{ tackler }} --input.fs.dir files1  >outputs/balances-tackler-files1.txt


# Generate one 100k txns single file journal
initialize-100k:
    cp /dev/null files1/100k.journal
    cp /dev/null files1/100k.beancount
    for i in `seq 1 10`; do cat ../10k/files1/10k.journal >> files1/100k.journal; done
    for i in `seq 1 10`; do cat ../10k/files1/10k.beancount >> files1/100k.beancount; done

